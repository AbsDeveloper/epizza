buildscript {
    ext {
        springBootVersion = '1.4.0.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") 
    }
}

plugins {
    id "org.asciidoctor.convert" version "1.5.2"
}

ext {
    snippetsDir = file('build/generated-snippets')
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot' 

version = '0.0.1-SNAPSHOT'

ext {
    snippetsDir = file('build/generated-snippets')
}

asciidoctor {
    sources {
        include '*.adoc'
    }

    attributes 'snippets': snippetsDir
    attributes 'version': project.version

    inputs.dir snippetsDir
    dependsOn test
}

jar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
    baseName = 'catalog'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}


dependencies {
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-data-rest")
    compile("com.epages.microservice.handson:shared:0.+")
    runtime("com.h2database:h2")

    testRuntime("org.springframework.data:spring-data-rest-hal-browser")
    testCompile('com.epages:restdocs-wiremock:0.7.8')
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
    testRuntime("com.jayway.jsonpath:json-path:2.0.0")
    testCompile("org.springframework.boot:spring-boot-starter-test") 
}

test {
    systemProperty 'org.springframework.restdocs.outputDir', snippetsDir
    outputs.dir snippetsDir
}

eclipse {
    classpath {
         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}
