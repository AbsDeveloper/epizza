buildscript {
    ext {
        springBootVersion = '1.4.0.RELEASE'
    }
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath "epizza:gradle-plugins:0.1.0"
        classpath "io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "org.asciidoctor.convert" version "1.5.2"
}
ext {
    snippetsDir = file('build/generated-snippets')
}

apply plugin: 'epizza-java'
apply plugin: 'epizza-docker'
apply plugin: 'spring-boot'

version = '0.0.1-SNAPSHOT'

asciidoctor {
    sources {
        include '*.adoc'
    }

    attributes 'snippets': snippetsDir
    attributes 'version': project.version

    inputs.dir snippetsDir
    dependsOn test
}

jar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
    baseName = 'order'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    all*.exclude module: 'aspectjrt'            // included in org.aspectj:aspectjweaver
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-data-rest")
    compile("org.springframework.boot:spring-boot-starter-actuator")

    compile("epizza:shared:0.+")
    compile('org.zalando:jackson-datatype-money:0.9.0')
    compileOnly("org.projectlombok:lombok")
    runtime("com.h2database:h2")
    runtime("javax.el:javax.el-api:2.2.4")
    runtime("org.glassfish.web:javax.el:2.2.4")

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("org.springframework.restdocs:spring-restdocs-mockmvc")
    testCompile("com.epages:restdocs-wiremock:0.7.8")
    testCompile("org.assertj:assertj-core:3.1.0")
    testCompile("org.mockito:mockito-core:1.+")
    testRuntime("com.jayway.jsonpath:json-path:2.0.0")
    testRuntime("org.springframework.data:spring-data-rest-hal-browser")
}

test {
    systemProperty 'org.springframework.restdocs.outputDir', snippetsDir
    outputs.dir snippetsDir
}
