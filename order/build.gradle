buildscript {
    ext {
        springBootVersion = '1.4.0.RELEASE'
    }
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenLocal()
    }
    dependencies {
        classpath("epizza:gradle-plugins:0.1.0")
        classpath("io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('net.ltgt.gradle:gradle-apt-plugin:0.9')
    }
}

apply plugin: 'epizza-java'
apply plugin: 'epizza-docker'
apply plugin: 'spring-boot'
apply plugin: 'epizza-restdocs'
apply plugin: 'net.ltgt.apt'

version = '0.0.1-SNAPSHOT'
group = 'epizza'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenLocal()
}

dependencyManagement.imports {
    mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Brixton.SR5"
    mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-data-rest")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.javamoney:moneta:1.1")
    compile("com.google.guava:guava:19.0")

    runtime("org.springframework.cloud:spring-cloud-starter-config")
    runtime("com.h2database:h2")

    compile("epizza:messaging-boot-starter:0.+")
    compile("org.hibernate:hibernate-java8")
    compile("org.zalando:jackson-datatype-money:0.9.0")
    compile("com.querydsl:querydsl-jpa:4.1.4")
    compileOnly("org.projectlombok:lombok")

    // APT
    apt("org.projectlombok:lombok")
    apt('com.querydsl:querydsl-apt:4.1.4:jpa') {
        exclude group: 'javax.jdo'
    }
    apt('org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final')

    // test
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("org.springframework.restdocs:spring-restdocs-mockmvc")
    testCompile("com.epages:restdocs-wiremock:0.7.8")
    testCompile("org.assertj:assertj-core:3.1.0")
    testCompile("org.mockito:mockito-core:1.+")
    testCompile('com.epages:restdocs-wiremock:0.7.10')
    testCompileOnly("org.projectlombok:lombok")
    testRuntime("com.jayway.jsonpath:json-path:2.0.0")
    testRuntime("org.springframework.data:spring-data-rest-hal-browser")
}
ext {
    generatedDir = 'src/main/generated'
}

compileJava {
    generatedSourcesDestinationDir = generatedDir
}

idea {
    module {
        sourceDirs += file(generatedDir)
    }
}

clean.doLast {
    file(generatedDir).list({ File dir, String name ->
        return ".gitkeep" != name
    }).each { f ->
        delete "${generatedDir}/${f}"
    }
}

