buildscript {
    ext {
        springBootVersion = '1.4.0.RELEASE'
    }
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath "epizza:gradle-plugins:0.1.0"
        classpath "io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "org.asciidoctor.convert" version "1.5.2"
}

ext {
    snippetsDir = file('build/generated-snippets')
}

apply plugin: 'spring-boot'
apply plugin: 'epizza-java'
apply plugin: 'epizza-docker'

version = '0.0.1-SNAPSHOT'

asciidoctor {
    sources {
        include '*.adoc'
    }

    attributes 'snippets': snippetsDir
    attributes 'version': project.version

    inputs.dir snippetsDir
    dependsOn test
}

jar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
    baseName = 'bakery'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencyManagement.imports {
    mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Brixton.SR5"
    mavenBom "org.springframework.boot:spring-boot-starter-parent:1.4.0.RELEASE"
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("epizza:messaging-boot-starter:0.+")
    compile("com.google.guava:guava:19.0")
    ["compileOnly", "testCompile"].each { "$it"("org.projectlombok:lombok") }

    runtime("com.h2database:h2")
    runtime('org.springframework.cloud:spring-cloud-starter-config')

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("org.assertj:assertj-core:3.1.0")
    testRuntime("com.jayway.jsonpath:json-path:2.0.0")
    testCompile("org.mockito:mockito-core:1.+")
}

test {
    systemProperty 'org.springframework.restdocs.outputDir', snippetsDir
    outputs.dir snippetsDir
}

eclipse {
    classpath {
         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}
